plugins {
    id 'fabric-loom' version "0.11-SNAPSHOT" apply false
    id "io.freefair.lombok" version "6.4.1" apply false
    id 'maven-publish'
    id 'java'
}

allprojects {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    group = rootProject.maven_group
    version = rootProject.mod_version

    // Disable Gradle Module Metadata (.module) generation
    // The module file doesn't include excludes of transitive dependencies
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}

subprojects {
    apply plugin: "fabric-loom"
    apply plugin: "io.freefair.lombok"
    apply plugin: "maven-publish"

    archivesBaseName = "$rootProject.archives_base_name-${project.name}"

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        // ensure that the encoding is set to UTF-8, no matter what the system default is
        // this fixes some edge cases with special characters not displaying correctly
        // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
        it.options.encoding = "UTF-8"

        it.options.release = 17
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        withSourcesJar()
        withJavadocJar()
    }

    jar {
        from "LICENSE"
    }

    javadoc {
        options {
            source = "17"
            encoding = "UTF-8"
            charSet = "UTF-8"
            memberLevel = JavadocMemberLevel.PROTECTED
            links = [
                    "https://docs.oracle.com/en/java/javase/17/docs/api/"
            ]
            // Disable the crazy super-strict doclint tool in Java 8
            addStringOption("Xdoclint:none", "-quiet")
        }
        failOnError false
    }

    repositories {
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
        maven {
            url "https://maven.terraformersmc.com/"
        }
        maven {
            url "https://maven.shedaniel.me/"
        }
        maven {
            url "https://maven.siphalor.de/"
        }
    }

    dependencies {
        minecraft("com.mojang:minecraft:$minecraft_version")
        mappings("net.fabricmc:yarn:$yarn_mappings:v2")
        modImplementation("net.fabricmc:fabric-loader:$loader_version")

        modImplementation("com.terraformersmc:modmenu:$modmenu_version")

        // Using modApi because api does not include dependency inside Maven pom file
        // see https://github.com/FabricMC/fabric-loom/issues/200
        // TODO: Test if this bug still applies
        modApi("org.spongepowered:configurate-hocon:$configurate_version")

        implementation("org.jetbrains:annotations:$jetbrains_annotations_version")
    }

    // configure the maven publication
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

task uberJar(type: Jar, dependsOn: subprojects.assemble) {
    manifest {
        from zipTree(project("base").tasks.remapJar.outputs.files.singleFile).find {
            it.name == "MANIFEST.MF"
        }
    }

    (subprojects - project("test-mod")).each { project ->
        from zipTree(project.tasks.remapJar.outputs.files.singleFile).matching {
            if(project.name != "base") {
                exclude "fabric.mod.json"
            }
        }
    }
}

build.dependsOn uberJar