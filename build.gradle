plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT' apply false
}

subprojects {
    apply plugin: "fabric-loom"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    group = rootProject.maven_group
    archivesBaseName = "${rootProject.archives_base_name}-${project.name}"
    version = rootProject.mod_version

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        // ensure that the encoding is set to UTF-8, no matter what the system default is
        // this fixes some edge cases with special characters not displaying correctly
        // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
        it.options.encoding = "UTF-8"
    }

    repositories {
        maven {
            url "https://maven.shedaniel.me/"
        }
    }

    dependencies {
        minecraft("com.mojang:minecraft:${rootProject.minecraft_version}")
        mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
        modImplementation("net.fabricmc:fabric-loader:${rootProject.loader_version}")

        modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}") {
            exclude(group: "net.fabricmc.fabric-api")
        }

        // Using modApi because api does not include dependency inside Maven pom file
        // see https://github.com/FabricMC/fabric-loom/issues/200
        modApi("org.spongepowered:configurate-hocon:${rootProject.configurate_version}")

        compileOnly("org.projectlombok:lombok:${rootProject.lombok_version}")
        annotationProcessor("org.projectlombok:lombok:${rootProject.lombok_version}")
    }
}
