import groovy.json.JsonSlurper

plugins {
    id 'fabric-loom' version "0.11-SNAPSHOT" apply false
    id "io.freefair.lombok" version "6.4.1" apply false
}

subprojects {
    apply plugin: "fabric-loom"
    apply plugin: "io.freefair.lombok"

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    group = rootProject.maven_group
    archivesBaseName = "$rootProject.archives_base_name-${project.name}"
    version = rootProject.mod_version

    ext.minecraftMinorVersion = rootProject.minecraft_version.substring(0, 4)
    ext.modName = new JsonSlurper().parse(file("src/main/resources/fabric.mod.json")).name

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        // ensure that the encoding is set to UTF-8, no matter what the system default is
        // this fixes some edge cases with special characters not displaying correctly
        // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
        it.options.encoding = "UTF-8"

        it.options.release = 17
    }

    repositories {
        maven {
            url "https://jitpack.io"
        }
        maven {
            url "https://maven.terraformersmc.com/"
        }
        maven {
            url "https://maven.shedaniel.me/"
        }
        maven {
            url "https://maven.siphalor.de/"
        }
        mavenCentral()
    }

    dependencies {
        minecraft("com.mojang:minecraft:${rootProject.minecraft_version}")
        mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
        modImplementation("net.fabricmc:fabric-loader:${rootProject.loader_version}")

        modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}") {
            exclude(group: "net.fabricmc.fabric-api")
        }
        modApi("de.siphalor:coat-$minecraftMinorVersion:${rootProject.coat_version}") {
            exclude(group: "net.fabricmc.fabric-api")
            exclude(group: "de.siphalor", module: "amecsapi-$minecraftMinorVersion")
            exclude(group: "com.github.astei", module: "lazydfu")
        }
        modImplementation("com.terraformersmc:modmenu:${rootProject.modmenu_version}")

        // Using modApi because api does not include dependency inside Maven pom file
        // see https://github.com/FabricMC/fabric-loom/issues/200
        modApi("org.spongepowered:configurate-hocon:${rootProject.configurate_version}")

        implementation("org.jetbrains:annotations:${rootProject.jetbrains_annotations_version}")
    }
}
